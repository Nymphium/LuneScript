import lune.base.Parser;

class Scope {
    pri let parent: Scope;
    pri let varName2DeclNodeMap: Map;
    __init( parent: Scope ) {
	self.parent = parent;
	self.varName2DeclNodeMap = {};
	return self;
    }
    pub add( name:str, declNode:stem ) {
	self.varName2DeclNodeMap[ name ] = declNode;
    }
    pub getDeclNode( name: str ) : stem {
	let node = self.varName2DeclNodeMap[ name ];
	if node {
	    return node;
	}
	if self.parent {
	    return self.parent.getDeclNode( name );
	}
	return nil;
    }
    pub getParent(): Scope {
	return self.parent;
    }
}

pub class TransUnit {
    pri let scope: Scope;
    __init() {
	self.scope = Scope.new( nil );
	return self;
    }
}

let type2typeId2 = {};

let typeIdSeed = 0;

pub let typeInfo = {};

class TypeInfo {
    pri let itemTypeInfo: TypeInfo;
    pri let typeId: int;
    pri let txt: str;
    __init( txt: str, typeId: int, itemTypeInfo: TypeInfo ) {
	self.txt = txt;
	self.typeId = typeId;
	self.itemTypeInfo = itemTypeInfo;
	return self;
    }

    pub getItemTypeInfo() : TypeInfo {
	return self.itemTypeInfo;
    }

    pub getTxt() : str {
	if self.txt {
	    return self.txt;
	}
	if self.itemTypeInfo {
	    return self.itemTypeInfo.getTxt() .. "[]";
	}
	return "";
    }

    pub static createBuiltin( idName: str, typeTxt: str ): TypeInfo {
	typeIdSeed = typeIdSeed + 1;
	let info = TypeInfo.new( typeTxt, typeIdSeed );
	type2typeId2[ typeTxt ] = info;
	typeInfo[ idName ] = info;
	return info;
    }

    pub static createList( itemTypeInfo: TypeInfo ): TypeInfo {
	typeIdSeed = typeIdSeed + 1;
	return TypeInfo.new( nil, typeIdSeed, itemTypeInfo );
    }

    pub static createArray( itemTypeInfo: TypeInfo ): TypeInfo {
	typeIdSeed = typeIdSeed + 1;
	return TypeInfo.new( nil, typeIdSeed, itemTypeInfo );
    }

    pub static createClass( className: str ): TypeInfo {
	typeIdSeed = typeIdSeed + 1;
	let info = TypeInfo.new( className, typeIdSeed );
	type2typeId2[ className ] = info;
	return info;
    }

    pub static createFunc( funcName: str ): TypeInfo {
	typeIdSeed = typeIdSeed + 1;
	let info = TypeInfo.new( funcName, typeIdSeed );
	type2typeId2[ funcName ] = info;
	return info;
    }
}

let typeInfoNone = TypeInfo.createBuiltin( "None", "" );
let typeInfoStem = TypeInfo.createBuiltin( "Stem", "stem" );
let typeInfoNil = TypeInfo.createBuiltin( "Nil", "nil" );
let typeInfoBool = TypeInfo.createBuiltin( "Bool", "bool" );
let typeInfoInt = TypeInfo.createBuiltin( "Int", "int" );
let typeInfoReal = TypeInfo.createBuiltin( "Real", "real" );
let typeInfoChar = TypeInfo.createBuiltin( "char", "char" );
let typeInfoString = TypeInfo.createBuiltin( "String", "str" );
let typeInfoMap = TypeInfo.createBuiltin( "Map", "Map" );
let typeInfoList = TypeInfo.createBuiltin( "List", "List" );
let typeInfoArray = TypeInfo.createBuiltin( "Array", "Array" );
let typeInfoForm = TypeInfo.createBuiltin( "Form", "form" );


let nodeKind2NameMap = {};
let nodeKindSeed = 0;
pub let nodeKind = {};

TransUnit.className2NodeMap = {};

fn regKind( name:str ): int {
    let kind = nodeKindSeed;
    nodeKindSeed = nodeKindSeed + 1;
    nodeKind2NameMap[ kind ] = name;
    nodeKind[ name ] = kind;
    return kind;
}

let nodeKindNone = regKind( 'None' );
let nodeKindImport = regKind( 'Import' );
let nodeKindRoot = regKind( 'Root' );
let nodeKindRefType = regKind( 'RefType' );
let nodeKindIf = regKind( 'If' );
let nodeKindWhile = regKind( 'While' );
let nodeKindRepeat = regKind( 'Repeat' );
let nodeKindFor = regKind( 'For' );
let nodeKindApply = regKind( 'Apply' );
let nodeKindForeach = regKind( 'Foreach' );
let nodeKindForsort = regKind( 'Forsort' );
let nodeKindReturn = regKind( 'Return' );
let nodeKindBreak = regKind( 'Break' );
let nodeKindExpList = regKind( 'ExpList' );
let nodeKindExpRef = regKind( 'ExpRef' );
let nodeKindExpOp2 = regKind( 'ExpOp2' );
let nodeKindExpCast = regKind( 'ExpCast' );
let nodeKindExpOp1 = regKind( 'ExpOp1' );
let nodeKindExpRefItem = regKind( 'ExpRefItem' );
let nodeKindExpCall = regKind( 'ExpCall' );
let nodeKindExpDDD = regKind( 'ExpDDD' );
let nodeKindExpParen = regKind( 'ExpParen' );
let nodeKindBlock = regKind( 'Block' );
let nodeKindStmtExp = regKind( 'StmtExp' );
let nodeKindRefField = regKind( 'RefField' );
let nodeKindDeclVar = regKind( 'DeclVar' );
let nodeKindDeclFunc = regKind( 'DeclFunc' );
let nodeKindDeclMethod = regKind( 'DeclMethod' );
let nodeKindDeclConstr = regKind( 'DeclConstr' );
let nodeKindDeclMember = regKind( 'DeclMember' );
let nodeKindDeclArg = regKind( 'DeclArg' );
let nodeKindDeclArgDDD = regKind( 'DeclArgDDD' );
let nodeKindDeclClass = regKind( 'DeclClass' );
let nodeKindLiteralNil = regKind( 'LiteralNil' );
let nodeKindLiteralChar = regKind( 'LiteralChar' );
let nodeKindLiteralInt = regKind( 'LiteralInt' );
let nodeKindLiteralReal = regKind( 'LiteralReal' );
let nodeKindLiteralArray = regKind( 'LiteralArray' );
let nodeKindLiteralList = regKind( 'LiteralList' );
let nodeKindLiteralMap = regKind( 'LiteralMap' );
let nodeKindLiteralString = regKind( 'LiteralString' );
let nodeKindLiteralBool = regKind( 'LiteralBool' );



let quotedChar2Code = {};
quotedChar2Code[ 'a' ] = 7;
quotedChar2Code[ 'b' ] = 8;
quotedChar2Code[ 't' ] = 9;
quotedChar2Code[ 'n' ] = 10;
quotedChar2Code[ 'v' ] = 11;
quotedChar2Code[ 'f' ] = 12;
quotedChar2Code[ 'r' ] = 13;
quotedChar2Code[ '\\' ] = 92;
quotedChar2Code[ '"' ] = 34;
quotedChar2Code[ "'" ] = 39;

fn nodeFilter( node: stem, filter: func, ... ) : stem {
    if not filter[ node.kind ] {
	error( string.format( "none filter -- %s",
			      TransUnit.getNodeKindName( node.kind ) ));
    }
    return filter[ node.kind ]( filter, node, ... );
}

pub fn getNodeKindName( kind: int ): str {
    return nodeKind2NameMap[ kind ];
}


pub fn TransUnit.createAST( parser: str ): Map {

    let rootInfo = {};
    rootInfo.childlen = {};
    self.ast = self.createNode(
	nodeKindRoot, { "lineNo": 0, "column": 0 }, typeInfoNone, rootInfo );
    self.parser = parser;
    self.moduleName2Info = {};
    self.analyzeStatement( rootInfo.childlen );

    let token = self.getTokenNoErr();
    if token {
	error( string.format( "unknown:%d:%d:(%s) %s",
			      token.pos.lineNo, token.pos.column,
			      Parser.getKindTxt( token.kind ), token.txt ) );
    }

    return self.ast;
}

fn TransUnit.createNoneNode(): stem {
    return self.createNode( nodeKindNone, { "lineNo": 0, "column": 0 },
			    typeInfoNone, {} );			    
}

fn TransUnit.createNode( kind: int, pos: stem, expType: TypeInfo, info: Map ): Map {
    if not getNodeKindName( kind ) {
	error( string.format( "%d:%d: not found nodeKind", pos.lineNo, pos.column ) );
    }
    return { "kind": kind, "pos": pos, "expType": expType,
	     "info": info, "filter": nodeFilter };
}

fn TransUnit.analyzeDecl(
    accessMode: str, staticFlag: bool, firstToken: stem, token: stem ): stem
{
    let staticFlag = false;

    if not staticFlag {
	if token.txt == "static" {
	    staticFlag = true;
	    token = self.getToken();
	}
    }

    if token.txt == "let" {
	return self.analyzeDeclVar( accessMode, staticFlag, firstToken );
    }
    elseif token.txt == "fn" {
	return self.analyzeDeclFunc( accessMode, staticFlag, false, token, nil );
    }
    elseif token.txt == "class" {
	return self.analyzeDeclClass( accessMode, token );
    }

    return nil;
}

fn TransUnit.analyzeStatement( stmtList: stem[], termTxt: str ) {
    while true {
	let token = self.getTokenNoErr();
	if not token {
	    break;
	}

	let statement = self.analyzeDecl( "pri", false, token, token );

	if not statement {
	    if token.txt == termTxt {
		self.pushback();
		break;
	    }
	    elseif token.txt == "pub" or token.txt == "pro" or
		token.txt == "pri" or token.txt == "global" or token.txt == "static"
	    {
		let accessMode = (token.txt ~= "static") and token.txt or "pri";
		let staticFlag = (token.txt == "static");

		let nextToken = nil;
		if token.txt ~= "static" {
		    nextToken = self.getToken();
		}
		else {
		    nextToken = token;
		}
		statement = self.analyzeDecl( accessMode, staticFlag, token, nextToken );
	    }
	    elseif token.txt == "{" {
		self.pushback();
		statement = self.analyzeBlock( "{" );
	    }
	    elseif token.txt == "if" {
		statement = self.analyzeIf( token );
	    }
	    elseif token.txt == "while" {
		statement = self.analyzeWhile( token );
	    }
	    elseif token.txt == "repeat" {
		statement = self.analyzeRepeat( token );
	    }
	    elseif token.txt == "for" {
		statement = self.analyzeFor( token );
	    }
	    elseif token.txt == "apply" {
		statement = self.analyzeApply( token );
	    }
	    elseif token.txt == "foreach" {
		statement = self.analyzeForeach( token, false );
	    }
	    elseif token.txt == "forsort" {
		statement = self.analyzeForeach( token, true );
	    }
	    elseif token.txt == "return" {
		let expList = self.analyzeExpList();
		self.checkNextToken( ";" );
		statement = self.createNode(
		    nodeKindReturn, token.pos, typeInfoNone, expList );
	    }
	    elseif token.txt == "break" {
		self.checkNextToken( ";" );
		statement = self.createNode(
		    nodeKindBreak, token.pos, typeInfoNone, nil );}
	    elseif token.txt == "import" {
		let moduleName = self.getToken();
		let module = moduleName.txt;
		let nextToken = {};
		while true {
		    nextToken = self.getToken();
		    if nextToken.txt == "." {
			nextToken = self.getToken();
			moduleName = nextToken.txt;
			module = "%s.%s" ( module, moduleName );
		    }
		    else {
			break;
		    }
		}
		self.checkToken( nextToken, ";" );
		self.moduleName2Info[ moduleName ] = _luneScript.loadModule( module );
		    
		statement = self.createNode(
		    nodeKindImport, token.pos, typeInfoNone, module );
	    }
	    else {
		self.pushback();
		let exp = self.analyzeExp();
		self.checkNextToken( ";" );
		statement = self.createNode(
		    nodeKindStmtExp, token.pos, typeInfoNone, exp );
	    }
	}

	if not statement {
	    break;
	}
	table.insert( stmtList, statement );
    }
}

fn TransUnit.pushback() {
    if self.pushbackToken {
	error( string.format( "multiple pushback:%d:%d: %s, %s",
			      self.currentToken.pos.lineNo,
			      self.currentToken.pos.column,
			      self.pushbackToken.txt, self.currentToken.txt ) );
    }
    self.pushbackToken = self.currentToken;
    self.currentToken = nil;
}

fn TransUnit.getToken( mess: str ): stem {
    let token = self.getTokenNoErr();
    if not token {
	return Parser.getEofToken();
    }
    self.currentToken = token;
    return self.currentToken;
}

fn TransUnit.getTokenNoErr(): stem {
    if self.pushbackToken {
	self.currentToken = self.pushbackToken;
	self.pushbackToken = nil;
	return self.currentToken;
    }

    let commentList = {};
    let token = nil;
    while true {
	token = self.parser.getToken();
	if not token {
	    break;
	}
	if token.kind ~= Parser.kind.Cmnt {
	    break;
	}
	table.insert( commentList, token );
    }

    if token {
	token.commentList = commentList;
    }
    self.currentToken = token;
    return token;
}

fn TransUnit.getSymbolToken(): stem {
    return self.checkSymbol( self.getToken() );
}

fn TransUnit.checkSymbol( token: stem ): stem {
    if token.kind ~= Parser.kind.Symb and
	token.kind ~= Parser.kind.Kywd and
	token.kind ~= Parser.kind.Type
    {
	self.error( "illegal symbol" );
    }
    return token;
}


fn TransUnit.error( mess: str ) {
    let pos = { "lineNo": 0, "column": 0 };
    let txt = "";
    if self.currentToken {
	pos = self.currentToken.pos;
	txt = self.currentToken.txt;
    }
    error( string.format( "%d:%d:(%s) %s", pos.lineNo, pos.column, txt, mess ) );
}

fn TransUnit.checkNextToken( txt: str ): stem {
    return self.checkToken( self.getToken(), txt );
}

fn TransUnit.checkToken( token: stem, txt: str ): stem {
    if not token or token.txt ~= txt {
	self.error( string.format( "not found -- %s", txt ) );
    }
    return token;
}

fn TransUnit.analyzeIf( token: stem ): stem {
    let list = {};
    table.insert(
	list, { "kind": "if", "exp": self.analyzeExp(),
		"block": self.analyzeBlock( "if" ) } );

    let nextToken = self.getToken();
    if nextToken.txt == "elseif" {
	while nextToken.txt == "elseif" {
	    table.insert(
		list, { "kind": "elseif", "exp": self.analyzeExp(),
			"block": self.analyzeBlock( "elseif" ) } );
	    nextToken = self.getToken();
	}
    }

    if nextToken.txt == "else" {
	table.insert(
	    list, { "kind": "else", "block": self.analyzeBlock( "else" ) } );
    }
    else {
	self.pushback();
    }

    return self.createNode(
	nodeKindIf, token.pos, typeInfoNone, list );
}

fn TransUnit.analyzeWhile( token: stem ): stem {
    let info = { "exp": self.analyzeExp(), "block": self.analyzeBlock( "while" ) };
    return self.createNode(
	nodeKindWhile, token.pos, typeInfoNone, info );
}

fn TransUnit.analyzeRepeat( token: stem ): stem {
    let info = { "block": self.analyzeBlock(), "exp": self.analyzeExp() };
    let node = self.createNode( nodeKindRepeat, token.pos, typeInfoNone, info );
    self.checkNextToken( ";" );
    return node;
}

fn TransUnit.analyzeFor( token: stem ): stem {
    let val = self.getToken();
    if val.kind ~= Parser.kind.Symb {
	self.error( "not symbol" );
    }
    self.checkNextToken( "=" );
    let exp1 = self.analyzeExp();
    self.checkNextToken( "," );
    let exp2 = self.analyzeExp();
    let token = self.getToken();
    let exp3 = nil;
    if token.txt == "," {
	exp3 = self.analyzeExp();
    }
    else {
	self.pushback();
    }

    let info = { "block": self.analyzeBlock( "for" ), "val": val,
		 "init": exp1, "to": exp2, "delta": exp3 };
    let node = self.createNode( nodeKindFor, token.pos, typeInfoNone, info );
    return node;
}

fn TransUnit.analyzeApply( token: stem ): stem {
    let varList = {};
    let nextToken = nil;
    repeat {
	let var = self.getToken();
	if var.kind ~= Parser.kind.Symb {
	    self.error( "illegal symbol" );
	}
	table.insert( varList, var );
	nextToken = self.getToken();
    } nextToken.txt ~= ",";
    self.checkToken( nextToken, "of" );

    let exp = self.analyzeExp();
    if exp.kind ~= nodeKindExpCall {
	self.error( "not call" );
    }

    let block = self.analyzeBlock( "apply" );

    let info = { "varList": varList, "exp": exp, "block": block };
    return self.createNode( nodeKindApply, token.pos, typeInfoNone, info );
}

fn TransUnit.analyzeForeach( token: stem, sortFlag: true ): stem {
    let valSymbol = nil;
    let keySymbol = nil;
    let nextToken = nil;
    for index = 1, 2 {
	let sym = self.getToken();
	if sym.kind ~= Parser.kind.Symb {
	    self.error( "illegal symbol" );
	}
	if index == 1 {
	    valSymbol = sym;
	}
	else {
	    keySymbol = sym;
	}
	nextToken = self.getToken();
	if nextToken.txt ~= "," {
	    break;
	}
    }
    self.checkToken( nextToken, "in" );

    let exp = self.analyzeExp();

    let block = self.analyzeBlock( "foreach" );

    let info = { "val": valSymbol, "key": keySymbol,
		 "exp": exp, "block": block, "sort": sortFlag };
    return self.createNode( sortFlag and nodeKindForsort or nodeKindForeach,
			    token.pos, typeInfoNone, info );	    
}


fn TransUnit.analyzeRefType(): stem {
    let firstToken = self.getToken();
    let token = firstToken;
    let refFlag = false;
    if token.txt == "&" {
	refFlag = true;
	token = self.getToken();
    }
    let mutFlag = false;
    if token.txt == "mut" {
	mutFlag = true;
	token = self.getToken();
    }
    let name = self.checkSymbol( token );
    let arrayMode = "no";
    token = self.getToken();
    if token.txt == '[' or token.txt == '[@' {
	if token.txt == '[' {
	    arrayMode = "list";
	}
	else {
	    arrayMode = "array";
	}
	token = self.getToken();
	if token.txt ~= ']' {
	    self.pushback();
	    self.checkNextToken( ']' );
	}
    }
    else {
	self.pushback();
    }

    return self.createNode(
	nodeKindRefType, firstToken.pos, typeInfoNone, 
	{ "name": name, "refFlag": refFlag, "mutFlag": mutFlag, "array": arrayMode } );
}


fn TransUnit.analyzeDeclMember(
    accessMode: str, staticFlag: bool, firstToken: stem ): stem {
    let varName = self.getSymbolToken();
    token = self.getToken();
    let refType = self.analyzeRefType();
    token = self.getToken();
    '' accessor
    self.checkToken( token, ";" );

    return self.createNode(
	nodeKindDeclMember, firstToken.pos, typeInfoNone, 
	{ "name": varName, "refType": refType,
	  "staticFlag": staticFlag, "accessMode": accessMode } );
}

fn TransUnit.analyzeDeclMethod(
    accessMode: str, staticFlag: bool,
    className: str, firstToken: stem, name: str ): stem
{
    let node = self.analyzeDeclFunc( accessMode, staticFlag, true, name, name );
    node.info.className = className;
    return node;
}

fn TransUnit.analyzeDeclClass( classAccessMode: str, classToken: stem ): stem {
    let name = self.getToken();
    self.checkNextToken( "{" );

    let fieldList = {};
    while true {
	let token = self.getToken();
	if token.txt == "}" {
	    break;
	}
	let accessMode = "pri";
	if token.txt == "pub" or token.txt == "pro" or
	    token.txt == "pri" or token.txt == "global"
	{
	    accessMode = token.txt;
	    token = self.getToken();
	}
	let staticFlag = false;
	if token.txt == "static" {
	    staticFlag = true;
	    token = self.getToken();
	}
	if token.txt == "let" {
	    table.insert( fieldList,
			  self.analyzeDeclMember( accessMode, staticFlag, token ) );
	}
	else {
	    table.insert(
		fieldList,
		self.analyzeDeclMethod(
		    accessMode, staticFlag, name, token, token ) );
	}
    }

    let typeInfo = TypeInfo.createClass( name );
    let node = self.createNode(
	nodeKindDeclClass, classToken.pos, typeInfoNone, 
	{ "accessMode": classAccessMode, "name": name, "fieldList": fieldList } );
    self.className2NodeMap[ name.txt ] = node;
    return node;
}

fn TransUnit.analyzeDeclFunc(
    accessMode: str, staticFlag: bool,
    methodFlag: bool, firstToken: stem, name: stem ) : stem
{
    let argList = {};
    let token = self.getToken();
    if not name {
	if token.txt ~= "(" {
	    name = self.checkSymbol( token );
	    token = self.getToken();
	}
    }
    else {
	self.checkSymbol( name );
    }
    let className:stem = nil;
    if token.txt == "." {
	methodFlag = true;
	className = name;
	name = self.getSymbolToken();
	token = self.getToken();
    }

    self.checkToken( "(" );

    let kind = nodeKindDeclConstr;
    if methodFlag {
	if name.txt == "__init" {
	    kind = nodeKindDeclConstr;
	}
	else {
	    kind = nodeKindDeclMethod;
	}
    }
    else {
	kind = nodeKindDeclFunc;
    }

    repeat {
	let argName = self.getToken();
	if argName.txt == ")" {
	    token = argName;
	    break;
	}
	elseif argName.txt == "..." {
	    table.insert(
		argList,
		self.createNode(
		    nodeKindDeclArgDDD, argName.pos, typeInfoNone, argName ) );
	}
	else {
	    self.checkSymbol( argName );

	    self.checkNextToken( ":" );
	    let refType = self.analyzeRefType();
	    let arg = self.createNode( nodeKindDeclArg, argName.pos, typeInfoNone, 
				       { "name": argName, "argType": refType } );
	    table.insert( argList, arg );
	}
	token = self.getToken();
    } token.txt ~= ",";

    self.checkToken( token, ")" );

    token = self.getToken();
    let typeList = {};
    if token.txt == ":" {
	repeat {
	    table.insert( typeList, self.analyzeRefType() );
	    token = self.getToken();
	} token.txt ~= ",";
    }

    self.pushback();
    let body = self.analyzeBlock( "func" );
    let info = { "name": name, "argList": argList, "staticFlag": staticFlag,
		 "retTypeList": typeList, "body": body, "accessMode": accessMode };

    let node = self.createNode( kind, firstToken.pos, typeInfoNone, info );

    if className {
	let classNode = self.className2NodeMap[ className.txt ];
	info.className = className;
    }

    return node;
}

fn TransUnit.analyzeBlock( blockKind: str, scope: Scope ): stem {
    let token = self.checkNextToken( "{" );

    if not scope {
	let work = Scope.new( self.scope );
	self.scope = work;
    }

    let stmtList = {};
    self.analyzeStatement( stmtList, "}" );

    self.checkNextToken( "}" );

    if not scope {
	self.scope = self.scope.getParent();
    }

    let node = self.createNode( nodeKindBlock, token.pos, typeInfoNone, 
				{ "kind": blockKind, "stmtList": stmtList } );
    
    return node;
}

fn TransUnit.analyzeDeclVar(
    accessMode: str, staticFlag: bool, firstToken: stem ): stem {
    let varList = {};
    let token = nil;
    repeat {
	let varName = self.getSymbolToken();
	token = self.getToken();
	if token.txt == ":" {
	    let refType = self.analyzeRefType();
	    token = self.getToken();
	}
	table.insert( varList, { "name": varName, "refType": refType } );
    } token.txt ~= ",";

    let expList = nil;
    if token.txt == "=" {
	expList = self.analyzeExpList();
    }

    self.checkNextToken( ";" );

    let declVarInfo = { "accessMode": accessMode,
			"varList": varList, "expList": expList };
    return self.createNode( nodeKindDeclVar, firstToken.pos, typeInfoNone, declVarInfo );
}

fn TransUnit.analyzeExpList(): stem {
    let expList = {};
    let firstExp = nil;
    repeat {
	let exp = self.analyzeExp();
	if not firstExp {
	    firstExp = exp;
	}
	table.insert( expList, exp );
	let token = self.getToken();
    } token.txt ~= ",";

    self.pushback();

    return self.createNode( nodeKindExpList, firstExp.pos, typeInfoNone, expList );
}

fn TransUnit.analyzeListConst( token: stem ): stem {
    let nextToken = self.getToken();
    let expList = nil;
    if nextToken.txt ~= "]" {
	self.pushback();
	expList = self.analyzeExpList();
	self.checkNextToken( "]" );
    }
    let kind = nodeKindLiteralArray;
    if token.txt == '[' {
	kind = nodeKindLiteralList;
    }
    return self.createNode( kind, token.pos, typeInfoList, expList );
}

fn TransUnit.analyzeMapConst( token: stem ): stem {
    let nextToken = nil;
    let map = {};
    let pairList = {};
    repeat {
	nextToken = self.getToken();
	if nextToken.txt == "}" {
	    break;
	}
	self.pushback();

	let key = self.analyzeExp();
	self.checkNextToken( ":" );
	let val = self.analyzeExp();
	table.insert( pairList, { "key": key, "val": val } );
	map[ key ] = val;
	nextToken = self.getToken();
    } nextToken.txt ~= ",";

    self.checkToken( nextToken, "}" );
    return self.createNode( nodeKindLiteralMap, token.pos, typeInfoMap, 
			    { "map": map, "pairList": pairList } );
}

fn TransUnit.analyzeExpRefItem( token: stem, exp: stem ): stem {
    let indexExp = self.analyzeExp();
    self.checkNextToken( "]" );

    let info = { "val": exp, "index": indexExp };
    return self.createNode( nodeKindExpRefItem, token.pos, typeInfoNone, info );
}

fn TransUnit.analyzeExpCont( firstToken: stem, exp: stem ): stem {
    let nextToken = self.getToken();
    repeat {
	let matchFlag = false;
	if nextToken.txt == "[" {
	    matchFlag = true;
	    exp = self.analyzeExpRefItem( nextToken, exp );
	    nextToken = self.getToken();
	}
	if nextToken.txt == "(" {
	    matchFlag = true;
	    let work = self.getToken();
	    let expList = nil;
	    if work.txt ~= ")" {
		self.pushback();
		expList = self.analyzeExpList();
		self.checkNextToken( ")" );
	    }
	    let info = { "func": exp, "argList": expList };

	    exp = self.createNode( nodeKindExpCall, firstToken.pos, typeInfoNone, info );
	    nextToken = self.getToken();
	}
    } not matchFlag;

    if nextToken.txt == "." {
	return self.analyzeExpSymbol(
	    firstToken, self.getToken(), "field", exp );
    }

    self.pushback();
    return exp;

}

fn TransUnit.analyzeExpSymbol(
    firstToken: stem, token: stem, mode: str, prefixExp: stem ): stem {
    let exp = nil;

    if mode == "field" {
	let info = { "field": token, "prefix": prefixExp };
	exp = self.createNode( nodeKindRefField, firstToken.pos, typeInfoNone, info );
    }
    elseif mode == "symbol" {
	exp = self.createNode( nodeKindExpRef, firstToken.pos, typeInfoNone, token );
    }
    elseif mode == "fn" {
	exp = self.analyzeDeclFunc( "pri", false, false, token, nil );
    }
    else {
	self.error( "illegal mode", mode );
    }

    return self.analyzeExpCont( firstToken, exp );
}


fn TransUnit.analyzeExp( skipOp2Flag: bool ): stem {
    let firstToken = self.getToken();
    let token = firstToken;
    let exp = nil;

    if token.kind == Parser.kind.Dlmt {
	if token.txt == "..." {
	    return self.createNode( nodeKindExpDDD, firstToken.pos, typeInfoNone, token );
	}

	if token.txt == '[' or token.txt == '[@' {
	    return self.analyzeListConst( token );
	}
	if token.txt == '{' {
	    return self.analyzeMapConst( token );
	}
	if token.txt == "(" {
	    exp = self.analyzeExp( false );
	    self.checkNextToken( ")" );
	    exp = self.createNode( nodeKindExpParen, firstToken.pos, typeInfoNone, exp );
	    exp = self.analyzeExpCont( firstToken, exp );
	}
    }

    if token.kind == Parser.kind.Ope and Parser.isOp1( token.txt ) {
	'' 単項演算
	exp = self.analyzeExp( true );
	let typeInfo = typeInfoNone;
	if token.txt == "-" {
	    typeInfo = exp.expType;
	}	
	elseif token.txt == "#" {
	    typeInfo = typeInfoInt;
	}
	elseif token.txt == "not" {
	    typeInfo = typeInfoBool;
	}
	else {
	    self.error( "unknown op1" );
	}
	    
	exp = self.createNode( nodeKindExpOp1, firstToken.pos, typeInfo, 
			       { "op": token, "exp": exp } );   
	return self.analyzeExpOp2( firstToken, exp );
    }


    if token.kind == Parser.kind.Int {
	exp = self.createNode( nodeKindLiteralInt, firstToken.pos, typeInfoInt, 
			       { "token": token, "num": tonumber( token.txt ) } );
    }
    elseif token.kind == Parser.kind.Real {
	exp = self.createNode( nodeKindLiteralReal, firstToken.pos, typeInfoReal, 
			       { "token": token, "num": tonumber( token.txt ) } );
    }
    elseif token.kind == Parser.kind.Char {
	let num = 0;
	if #(token.txt) == 1 {
	    num = token.txt.byte( 1 );
	}
	else {
	    num = quotedChar2Code[ token.txt.sub( 2, 2 ) ];
	}
	exp = self.createNode( nodeKindLiteralChar, firstToken.pos, typeInfoChar, 
			       { "token": token, "num": num } );
    }
    elseif token.kind == Parser.kind.Str {
	let nextToken = self.getToken();
	let formatArgList = {};
	if nextToken.txt == "(" {
	    repeat {
		let arg = self.analyzeExp();
		table.insert( formatArgList, arg );
		nextToken = self.getToken();
	    } nextToken.txt ~= ",";
	    self.checkToken( nextToken, ")" );
	    nextToken = self.getToken();
	}
	exp = self.createNode( nodeKindLiteralString, firstToken.pos, typeInfoString, 
			       { "token": token, "argList": formatArgList } );
	token = nextToken;
	if token.txt == "[" {
	    exp = self.analyzeExpRefItem( token, exp );
	}
	else {
	    self.pushback();
	}
    }
    elseif token.txt == "fn" {
	exp = self.analyzeExpSymbol( firstToken, token, "fn", token );
    }
    elseif token.kind == Parser.kind.Symb {
	exp = self.analyzeExpSymbol( firstToken, token, "symbol", token );
    }
    elseif token.kind == Parser.kind.Type {
	exp = self.createNode( nodeKindExpRef, firstToken.pos, typeInfoNone, token );
    }
    elseif token.txt == "true" or token.txt == "false" {
	exp = self.createNode( nodeKindLiteralBool, firstToken.pos, typeInfoBool, token );
    }
    elseif token.txt == "nil" {
	exp = self.createNode( nodeKindLiteralNil, firstToken.pos, typeInfoNil, token );
    }

    if not exp {
	self.error( "illegal exp" );
    }

    if skipOp2Flag {
	return exp;
    }

    return self.analyzeExpOp2( firstToken, exp );
}

fn TransUnit.analyzeExpOp2( firstToken: stem, exp: stem ): stem {
    let nextToken = self.getToken();
    while true {
	if nextToken.txt == "@" {
	    let castType = self.analyzeRefType();
	    let info = { "exp": exp, "castType": castType };
	    exp = self.createNode( nodeKindExpCast, firstToken.pos, typeInfoNone, info );
	}
	elseif nextToken.kind == Parser.kind.Ope {
	    if Parser.isOp2( nextToken.txt ) {
		let exp2 = self.analyzeExp(
		    ( nextToken.txt == "and" ) or ( nextToken.txt == "*" ) );
		let info = { "op": nextToken, "exp1": exp, "exp2": exp2 };
		exp = self.createNode( nodeKindExpOp2, firstToken.pos, typeInfoNone, info );
	    }
	    else {
		self.error( "illegal op" );
	    }
	}
	else {
	    self.pushback();
	    return exp;
	}
	nextToken = self.getToken();
    }
}
